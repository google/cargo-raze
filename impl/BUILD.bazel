load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("//third_party/cargo:crates.bzl", "all_crate_deps")

package(default_visibility = ["//visibility:public"])

rust_library(
    name = "cargo_raze",
    srcs = glob(
        ["src/**/*.rs"],
        exclude = ["src/bin/cargo-raze.rs"],
    ),
    data = glob(
        ["src/**/*.template"],
        exclude = ["src/testing/**/*.template"],
    ),
    edition = "2018",
    proc_macro_deps = all_crate_deps(proc_macro = True),
    deps = all_crate_deps(),
)

rust_binary(
    name = "cargo_raze_bin",
    srcs = [
        "src/bin/cargo-raze.rs",
    ],
    edition = "2018",
    proc_macro_deps = all_crate_deps(proc_macro = True),
    deps = [":cargo_raze"] + all_crate_deps(),
)

_TEST_DATA = glob([
    "src/**/*.template",
    "src/testing/cargo_tree/*.txt",
]) + [
    "//tools:cargo",
    "//tools:rustc",
]

_TEST_ENV = {
    "CARGO": "$(rootpath //tools:cargo)",
    "CARGO_HOME": "$(rootpath //tools:cargo).home",
    "RUSTC": "$(rootpath //tools:rustc)",
}

rust_test(
    name = "cargo_raze_lib_test",
    crate = ":cargo_raze",
    data = _TEST_DATA,
    edition = "2018",
    env = _TEST_ENV,
    proc_macro_deps = all_crate_deps(proc_macro_dev = True),
    deps = all_crate_deps(normal_dev = True),
)

rust_test(
    name = "cargo_raze_bin_test",
    crate = ":cargo_raze_bin",
    data = _TEST_DATA,
    edition = "2018",
    env = _TEST_ENV,
    proc_macro_deps = all_crate_deps(proc_macro_dev = True),
    deps = all_crate_deps(normal_dev = True),
)
